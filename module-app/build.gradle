apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    getPublic {
        file rootProject.file('public.properties')
    }
    getLocal {
        file rootProject.file('local.properties')
    }
}

android {
    compileSdkVersion Config.compileSdkVersion
    buildToolsVersion Config.buildToolsVersion
    defaultConfig {
        applicationId Config.applicationId
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled false

        buildConfigField "String", "OPENHUB_CLIENT_ID", (buildProperties.getLocal['openhub_client_id'] | buildProperties.getPublic['openhub_client_id']).string
        buildConfigField "String", "OPENHUB_CLIENT_SECRET", (buildProperties.getLocal['openhub_client_secret'] | buildProperties.getPublic['openhub_client_secret']).string
        buildConfigField "String", "BUGLY_ID", (buildProperties.getLocal['bugly_appid'] | buildProperties.getPublic['bugly_appid']).string
        buildConfigField "String", "DEBUG_BUGLY_ID", (buildProperties.getLocal['debug_bugly_appid'] | buildProperties.getPublic['debug_bugly_appid']).string

        ndk {
//            abiFilters 'armeabi', 'armeabi-v7a', 'x86'
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    signingConfigs  {
        release {
            storeFile(file((buildProperties.getLocal['sign_path'] | buildProperties.getPublic['sign_path']).string))
            storePassword((buildProperties.getLocal['store_password'] | buildProperties.getPublic['store_password']).string)
            keyAlias((buildProperties.getLocal['key_alias'] | buildProperties.getPublic['key_alias']).string)
            keyPassword((buildProperties.getLocal['key_password'] | buildProperties.getPublic['key_password']).string)
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    flavorDimensions "default"

    productFlavors {
        normal {
            dimension "default"
            // add app_name int string.xml
            resValue "string", "app_name", "UUHub"
        }
        fullName {
            dimension "default"
            // add app_name int string.xml
            resValue "string", "app_name", "UUHub"
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [BUGLY_APP_CHANNEL_VALUE: name]
    }

}

repositories {
    flatDir {
        dirs 'libs' //aar
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation BuildDependencies.junit
    implementation AndroidX.appCompat
    implementation AndroidX.recyclerview
    implementation AndroidX.material
    implementation AndroidX.cardview
    implementation AndroidX.preference
    implementation AndroidX.constraintlayout
    implementation AndroidX.annotationX
    implementation AndroidX.browser

    api project(':lib_base')
    api project(':lib_githubsdk')

 //   implementation "com.android.support.constraint:constraint-layout:${Versions.CONSTRAINT_VERSION}"
//    compile "com.android.support:multidex:${MULTIDEX_VERSION}"

    implementation "com.thirtydegreesray:dataautoaccess:${Versions.DATAAUTOACCESS_VERSION}"
    annotationProcessor "com.thirtydegreesray:dataautoaccess-compiler:${Versions.DATAAUTOACCESS_VERSION}"

    implementation "com.jakewharton:butterknife:${Versions.BUTTERKNIFE_VERSION}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${Versions.BUTTERKNIFE_VERSION}"

    //数据库工具
    implementation "org.greenrobot:greendao:${Versions.GREEN_DAO_VERSION}"
    //事件总线
    implementation "org.greenrobot:eventbus:${Versions.EVENT_BUS_VERSION}"
    //文件下载


    implementation BuildDependencies.rxjava2
    implementation BuildDependencies.rxAndroid
    implementation BuildDependencies.retrofit
    implementation BuildDependencies.retrofitConverterGson
    implementation BuildDependencies.retrofitAdapterRxjava2
    implementation (BuildDependencies.retrofitConvertXml){
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    implementation "com.squareup.okhttp3:logging-interceptor:${Versions.OKHTTP_VERSION}"

    //dagger依赖注入
    implementation "com.google.dagger:dagger:${Versions.DAGGER_VERSION}"
    annotationProcessor "com.google.dagger:dagger-compiler:${Versions.DAGGER_VERSION}"

    implementation "com.tbruyelle.rxpermissions2:rxpermissions:${Versions.RX_PERMISSION_VERSION}"

    implementation "com.github.bumptech.glide:glide:${Versions.GLIDE_VERSION}"
    annotationProcessor "com.github.bumptech.glide:compiler:${Versions.GLIDE_VERSION}"

//    compile "com.makeramen:roundedimageview:${ROUNDED_IMAGEVIEW_VERSION}"
    implementation "de.hdodenhof:circleimageview:${Versions.CIRCLE_IMAGEVIEW_VERSION}"

    implementation "com.github.GrenderG:Toasty:${Versions.TOASTY_VERSION}"
    implementation "com.github.daniel-stoneuk:material-about-library:${Versions.MATERIAL_ABOUT_VERSION}"
    implementation "com.unstoppable:submitbutton:${Versions.SUBMIT_BUTTON_VERSION}"

    implementation "com.afollestad.material-dialogs:core:${Versions.MATERIAL_DIALOG_VERSION}"
    implementation "com.afollestad.material-dialogs:commons:${Versions.MATERIAL_DIALOG_VERSION}"

    implementation "com.zzhoujay.richtext:richtext:${Versions.RICKTEXT_VERSION}"
    implementation 'com.zzhoujay.glideimagegetter:glideimagegetter:1.0.5'

    implementation "com.orhanobut:logger:${Versions.LOGGER_VERSION}"
    implementation "com.timehop.stickyheadersrecyclerview:library:${Versions.STICKY_HEAD_VERSION}"

    implementation "org.jsoup:jsoup:${Versions.JSOUP_VERSION}"
}
